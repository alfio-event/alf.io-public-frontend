plugins {
  id "com.moowork.node" version "1.3.1"
  id 'com.github.ben-manes.versions' version '0.27.0'
}

apply plugin:'java'
apply plugin: 'maven'

node {
  version = '10.15.3'
  npmVersion = '6.4.1'
  download = true
  workDir = file("${project.projectDir}/node")
  nodeModulesDir = file("${project.projectDir}/")
}

task npmClean(type: Delete) {
  final def webDir = "${rootDir}/frontend"
  delete "${webDir}/node"
  delete "${webDir}/node_modules"
  delete "${webDir}/dist"
  delete "${webDir}/coverage"
}

// clean task for npm
clean.dependsOn(npmClean)

// build task for npm
processResources.dependsOn(npm_install)
processResources.dependsOn(npm_run_build)

jar {
  archiveBaseName.set('alfio-public-frontend')
  version = "${project.version}"
  into("META-INF/resources/webjars/${archiveBaseName.get()}/${project.version}") {
      from 'dist'
  }
}

// run all task
task runAll {}
runAll.dependsOn(npm_run_start)

install {
  repositories.mavenInstaller {
    pom.project {
      licenses {
        license {
          name 'GNU General Public License, Version 3'
          url 'https://www.gnu.org/licenses/gpl-3.0.en.html'
          distribution 'repo'
        }
      }
    }
  }
}
